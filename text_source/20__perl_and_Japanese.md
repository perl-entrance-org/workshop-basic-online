# Perl と日本語

---

## Perl と日本語

これまで、Perl でプログラムの基礎を学習してきました。

Perl を学習する上での最初の大きな壁はリファレンスですが、それと共に立ち塞がるのが日本語です。

ここからは Perl入学式 での学習環境（linux/mac/msys2）を前提に学習していきます。

---

## Perl と日本語

### プログラム中の日本語

早速試してみましょう。`length` 関数は引数の文字数を返します。

半角文字列 `hello` であれば、 5 と返します。

```perl
#!/usr/bin/env perl
use strict;
use warnings;

my $greet = 'hello';

print 'length: ' . length($greet) . "\n";   # 5
```

[Perlの組み込み関数 length の翻訳 - perldoc.jp](https://perldoc.jp/func/length)

---

## Perl と日本語

### プログラム中の日本語

しかし、日本語で試すと意図しない結果になります。

```perl
#!/usr/bin/env perl
use strict;
use warnings;

my $greet = 'こんにちは';

print 'length: ' . length($greet) . "\n";   # 15
```

---

## Perl と日本語

### バイト列（バイナリ文字列）

これは、Perl が 'こんにちは' という日本語の文字列を、人間が読むように「5 文字の平仮名」とは認識しないためです。

Perl は入力、プログラム中の文字列、出力を **バイト列（バイナリ文字列）** として扱います。

この 'こんにちは' という文字列は Perl からは以下のような16進数の連なりとして見えています。

`E3 81 93 E3 82 93 E3 81 AB E3 81 A1 E3 82 AF`

このため、`length` 関数は 15 文字と文字数を返したのです。

---

## Perl と日本語

### テキスト文字列

プログラム中にある「こんにちは」という文字列をひらがな 5 文字として認識するためには、`use utf8` を利用します。

この `use utf8` によって、Perl はバイト列（バイナリ文字列）を、 **テキスト文字列** として扱い、書いてあるままに解釈します。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

my $greet = 'こんにちは';

print 'length: ' . length($greet) . "\n";   # 5
```

---

## Perl と日本語

### `use utf8` がいらない場合

プログラムは半角英語と数字、記号の組み合わせで書かれています。

正規表現風に書くと、文字は `a-zA-Z` で 26 文字 * 2 で 52 文字。これに数字が `0-9` で 10 文字、`+-/%\?=$@% .,` などの記号が数十種類あります。

これらは 2 桁の 16 進数（ 1 バイト）で表される 256 個の範囲で割り当てられています。一例を挙げるとこのような感じです。

```perl
a => 61 # 16進数の61
z => 7a # 16進数の7a
```

このため、英数字と記号だけでプログラムを書いている場合は、`use utf8` をしなくても、問題はありません。

---

## Perl と日本語

### `use utf8` が必要な場合

しかし、1 バイトで表現できる 256 個の空間では、膨大な数の日本語（ひらがな、カタカナ、漢字）を表現するに足りません。

このため、日本語 1 文字に複数のバイトを割り当てることで日本語を処理することにしました。このルールが **エンコーディング** と呼ばれるものです。

```perl
こ => E3 81 93
ん => E3 82 93
に => E3 81 AB
ち => E3 81 A1
は => E3 82 AF
```

`use utf8` を使うことで、Perl はプログラムに書かれている文字が `utf8` エンコード であることを前提に文字を処理するようになります。

---


## Perl と日本語

### 入力された日本語を正しく扱う

コマンドライン引数から「こんにちは」と入力した時も同様の問題が発生します。

`use utf8` を使うことで、Perl はプログラムに書かれた文字をテキスト文字列として解釈します。しかし、今回はコマンドライン引数から入力されるバイト列（バイナリ文字列）なので効果がありません。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8; # あってもなくても同じ

my $input = shift @ARGV;

print 'length: ' . length($input) . "\n"; # 引数に日本語を入れるとカウント失敗
```

---

## Perl と日本語

### 入力された日本語を正しく扱う

コマンドライン引数から入力されたバイト列（バイナリ文字列）をテキスト文字列にするには <ruby>`Encode`<rt>エンコード</rt></ruby> モジュールの <ruby>decode<rt>デコード</rt></ruby> を使います。

これで、正しく日本語入力を受け取ることができました。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use Encode;     # 追加

my $input = shift @ARGV;
my $decoded_input = decode('utf8', $input); # utf8 で デコードする

print 'length: ' . length($decoded_input) . "\n";
```

---

## Perl と日本語

### 出力する日本語を正しく扱う

これまで、「プログラム中に日本語が書いてある場合」「日本語の入力を受け付ける場合」の２つのパターンを学んできました。

最後は「プログラムで処理した日本語を出力する場合」です。

---

## Perl と日本語

### 出力する日本語を正しく扱う

`use utf8` を使った以下のプログラムで、日本語が代入されている `$greet` をそのまま `print` すると警告が出ます。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

my $greet = 'こんにちは';

print 'length: ' . length($greet) . "\n";   # 5
print $greet . "\n";   # Wide character in print at 〜
```

---

## Perl と日本語

### 出力する日本語を正しく扱う

これは `use utf8` することでテキスト文字列になった 'こんにちは' （を代入した `$greet`）を、テキスト文字列のまま表示しようとしたためです。

> Perl は入力、プログラム中の処理、出力をこの**バイト列（バイナリ文字列）**で扱います。


---

## Perl と日本語

### 出力する日本語を正しく扱う

入力を受け取った後にデコードをしたように、出力する前に Encode モジュールの <ruby>encode<rt>エンコード</rt></ruby> を行うことで、対象の文字列をバイト列（バイナリ文字列）に変換できます。

これで、警告がなくなりました。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8;
use Encode; # 追加

my $greet = 'こんにちは';

print 'length: ' . length($greet) . "\n";   # 5
print encode('utf8', $greet) . "\n";        # utf8 でエンコード こんにちは
```

---

## Perl と日本語

### 日本語の入出力まとめ

今まで見てきた通り、Perl の日本語処理は以下のようになります。

1. プログラム中に日本語を書いたり、日本語を対象にした正規表現などを書くときは `use utf8` してバイト列（バイナリ文字列）をテキスト文字列にする。

2. コマンドライン引数などで日本語の入力を受け付けて、それを 1 のような加工や、`use utf8` した日本語との比較をする場合には、 `decode` してバイト列（バイナリ文字列）をテキスト文字列にする。

3. 上記 1, 2 のような処理をした文字列を出力するときは、 `encode` してテキスト文字列をバイト列（バイナリ文字列）にする。

---

## Perl と日本語

### 日本語の入出力まとめ

日本語を入出力したり、単純な比較のみであれば、`use utf8` は不要です。

```perl
#!/usr/bin/env perl
use strict;
use warnings;

my $input = shift @ARGV;    # バイト列（バイナリ文字列）で受け取る

if ($input eq 'こんにちは'){ #  バイト列（バイナリ文字列）のまま比較する
    print "同じ文字列です";    # バイト列（バイナリ文字列）を出力する。
}else{
    print "違う文字列です";    # バイト列（バイナリ文字列）を出力する。
}
```

---

## Perl と日本語

### 日本語の入出力まとめ

しかし、テキストで学習した `length` や、以下のような日本語を対象にした正規表現を利用する場合には、エンコードの処理が必要です。

```perl
my $input = shift @ARGV;    # バイト列（バイナリ文字列）で受け取る

if ($input =~ /^(.)/ ){ # バイト列（バイナリ文字列）のまま正規表現！
                        # 正規表現 ^(.) で先頭の 1 文字をキャプチャする
                        # でも、decode していないから・・・？

    print "先頭の文字は $1 です";    # バイト列（バイナリ文字列）を出力する。
}
```

---

## 練習問題 `greet_and_length.pl`

以下のプログラムはコマンドライン引数に名前を入力すると、挨拶と文字数を表示するプログラムです。

これを改修して、コマンドライン引数に日本語を入力しても、正しく表示や文字数カウントができるようにしてください。`print` 文内の `$input` は各自で設定した変数に置き換えてください。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

my $input = shift @ARGV;

print 'こんにちは！' . $input . "さん。\n";
print 'あなたの名前は' . length($input) . "文字です。\n";
```

---

## 練習問題 `first_and_last_letter.pl`

以下のプログラムはコマンドライン引数に文字列を入力すると、先頭の文字と末尾の文字を教えてくれるプログラムです。

これを改修して、コマンドライン引数に日本語を入力しても、正しく表示ができるようにしてください。

```perl
#!/usr/bin/env perl
use strict;
use warnings;

my $input = shift @ARGV;    # バイト列（バイナリ文字列）で受け取る

if ($input =~ /^(.).*(.)$/){   # バイト列のまま正規表現！
                               # 入力された文字の先頭 1 字と末端 1 字をキャプチャ
    print "$1 ではじまって $2 で終わります";
    # バイト列（バイナリ文字列）を出力する
}
```

---

## 練習問題 `use_utf8.pl`

以下のプログラムはコマンドライン引数に `こんにちは` と入力しても、同じ文字列とは判定されません。また、`Wide character in print at 〜` の警告も表示されます。

これを `use utf8;` は残したまま改修して、正しく判定され、警告も出ないようにしてください。

```perl
#!/usr/bin/env perl
use strict;
use warnings;
use utf8;   # use utf8 しているので、プログラムに書かれているのはテキスト文字列

my $input = shift @ARGV;    # バイト列（バイナリ文字列）で受け取る

if ($input eq 'こんにちは'){ #  バイト列（バイナリ文字列）とテキスト文字列を比較？
    print "同じ文字列です";    # テキスト文字列を出力する。
}else{
    print "違う文字列です";    # テキスト文字列を出力する。
}
```

---

## 出来た方は、Discord のテキストチャットで「出来た！」とリアクションお願いします！

---

## 休憩 ＆ 質問 ＆ 雑談 タイム<br>（5 〜 10 分）
